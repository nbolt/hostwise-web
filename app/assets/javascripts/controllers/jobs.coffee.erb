JobsCtrl = ['$scope', '$http', '$timeout', '$interval', '$window', 'ngDialog', ($scope, $http, $timeout, $interval, $window, ngDialog) ->

  $scope.tabs = [{name:'open'},{name:'upcoming'},{name:'past'}]
  $scope.filter = {id:'recent',text:'Most Recent'}
  faye = null

  $scope.tab = (name) ->
    angular.element('#jobs .tabs .tab').removeClass 'active'
    angular.element('#jobs .tab-content .tab').removeClass 'active'
    angular.element("#jobs .tabs .tab.#{name}").addClass 'active'
    angular.element("#jobs .tab-content .tab.#{name}").addClass 'active'
    null

  $scope.filters = ->
    {
      dropdownCssClass: 'details'
      minimumResultsForSearch: -1
      data: [{id:'recent',text:'Most Recent'}]
      initSelection: (el, cb) ->
    }

  $scope.confirm_claim = (job) ->
    $scope.selected_job = job
    ngDialog.open template: 'claim-job-modal', className: 'claim full', scope: $scope

  $scope.close_modal = ->
    ngDialog.closeAll()

  $scope.claim = (job) ->
    $http.post("/jobs/#{job.id}/claim").success (rsp) ->
      $scope.close_modal()
      if rsp.success
        angular.element("#job-#{job.id}").fadeOut()
      else
        refresh_jobs(->
          angular.element("#job-#{job.id} .flash").css('opacity', 1).text rsp.message
          $timeout((-> angular.element("#job-#{job.id}").fadeOut()), 4000)
        )

  $scope.drop = (job) ->
    $http.post("/jobs/#{job.id}/drop").success (rsp) ->
      if rsp.success
        angular.element("#job-#{job.id}").fadeOut()
      else
        refresh_jobs()

  $scope.pricing_class = (job) ->
    if job.staging
      'staging'
    else if job.state_cd is 1
      'vip'
    else
      ''

  $scope.job_available = (job) -> job.first_job_of_day || job.contractor_hours + job.man_hours <= <%= MAX_MAN_HOURS %>

  $scope.jobs_available = (day) -> _(day[1]).filter((job) -> $scope.job_available job)[0]

  refresh_jobs = (cb) ->
    $scope.user_fetched.promise.then ->
      _($scope.tabs).each (tab) ->
        $http.get('/data/jobs', {params: {scope: tab.name, sort: $scope.filter.id}}).success (rsp) ->
          if tab.name == 'open'
            if !tab.days
              angular.element('.tab-content .tab:eq(0)').addClass 'active'
            tab.days = _(rsp).filter (day) -> $scope.jobs_available day
            _(tab.days).each (day) -> day[1] = _(day[1]).filter((job) -> $scope.job_available job)
          else
            tab.days = rsp
          tab.count = _(tab.days).reduce(((acc, day) -> acc + day[1].length), 0)
          _(tab.days).each (day) ->
            day.push {}
            date = moment.utc(day[0],"MM-DD-YY")
            date_text = date.format("ddd, MMM D")
            if date.month() == moment().month()
              if date.date() == moment().date()
                date_text += ', Today'
              else if date.date() == moment().date() + 1
                date_text += ', Tomorrow'
            day[2].date_text = date_text
          cb() if cb

  $scope.$watch 'filter', (n,o) -> if o
    refresh_jobs()

  load_faye = null
  load_faye = $interval((->
    if $window.faye_loaded
      $interval.cancel(load_faye)
      faye = new Faye.Client "http://pubsub.fanout.io/r/<%= ENV['FANOUT_ID'] %>/bayeux"
      faye.subscribe '/jobs', (data) -> refresh_jobs()
  ), 200)

]

app = angular.module('porter').controller('jobs', JobsCtrl)
